apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// this is the library version used when deploying the artifact
version = "1.0.0"

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 1
        versionName version
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

//repositories {
//    maven { url "http://dl.bintray.com/populov/maven" }
//    mavenCentral()
//    jcenter()
//}

dependencies {
//    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:support-v4:22+'
    compile 'com.android.support:appcompat-v7:22.2.0'

    compile 'com.google.code.gson:gson:2.3.+'

    compile 'com.cocosw:bottomsheet:0.+@aar'

    compile 'com.skyfishjy.ripplebackground:library:1.0.1'

    compile 'com.viewpagerindicator:library:2.4.1@aar'

    compile 'com.squareup.picasso:picasso:2.5.0'

    compile 'com.melnykov:floatingactionbutton:1.3.0'

    compile 'com.afollestad:material-dialogs:0.7.3.4'

    compile 'com.specyci:residemenu:1.6+'

    compile 'net.danlew:android.joda:2.7.2'

    compile 'com.github.gabrielemariotti.cards:cardslib-core:2.0.1'
    compile 'com.github.gabrielemariotti.cards:cardslib-cards:2.0.1'
    compile 'com.github.gabrielemariotti.cards:cardslib-recyclerview:2.0.1'
}

def siteUrl = 'https://github.com/weidaxu1988/dwAndroid'
def gitUrl = 'https://github.com/weidaxu1988/dwAndroid.git'
group = "com.dawei"

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'Simple Android Project Framework'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'weidaxu1988'
                        name 'Dawei'
                        email 'wdxu88@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "dwAndroid"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}
